#!/usr/bin/env perl
use strict;
use warnings;

use lib "lib";
use RetreatCenterDB;
use Util qw/
    model
    db_init
    email_letter
    empty
/;
my $c = db_init();
use Global qw/
    %string
/;
Global->init($c, 1, 1);
use Template;
use Date::Simple qw/
    today
/;

my $default_packet = 'MMC Guest Packet.pdf';
my $in_two_days_d8 = (today() + 2)->as_d8();
my $today = today()->format("%B %e, %Y"),
my $tt = Template->new({
    INTERPOLATE  => 1,
    INCLUDE_PATH => "root/static/templates/letter",
    EVAL_PERL    => 0,
});

# Registrations
my @regs = model($c, 'Registration')->search({
               date_start => $in_two_days_d8,
               cancelled  => { '!=' => 'yes' },
           });
REG:
for my $reg (@regs) {
    my $person = $reg->person();
    my $email = $person->email();
    if (! $email) {
        next REG;
    }
    my $program = $reg->program();
    if ($program->housing_not_needed()) {
        # likely an ONLINE program - no need to send the packet
        next REG;
    }
    my $packet = $program->alt_packet || $default_packet;
    my $gate_code = $program->summary->gate_code();
    my $balance = $reg->balance();
    my $prepay_link = '';
    if ($balance > 0) {
        my @req_payments = $reg->req_payments();
        if (@req_payments) {
            my $code = $req_payments[0]->code();
            $prepay_link = "https://www.mountmadonna.org/cgi-bin/"
                         . "req_pay?code=$code"
                         ;
        }
    }
    my $stash_href = {
        today       => $today,
        balance     => $balance,
        first       => $person->first(),
        gate_code   => $gate_code,
        prepay_link => $prepay_link,
        program     => 1,
    };
    my $html;
    $tt->process(
        'guest_packet.tt2',
        $stash_href,
        \$html,
    ) or die "error in processing template: " . $tt->error;
    sleep 1;        # so we aren't blacklisted as spammers?
    my $alt = $packet ne $default_packet? 'Alternate ': '';
    email_letter($c,
        from    => "$string{from_title} <$string{from}>",
        to      => $email,
        subject => 'Guest Packet for your visit to MMC',
        html    => $html,
        files_to_attach => [
            "/var/Reg/documents/$packet",
        ],
        activity_msg => sprintf("${alt}Packet sent for"
                              . " <a href='/registration/view/%s'>%s</a>",
                                $reg->id, $person->name),
    );
}

# Rentals
my @rentals = model($c, 'Rental')->search({
                  sdate => $in_two_days_d8,
                  cancelled => { '!=' => 'yes' },
              });
for my $rental (@rentals) {
    my $rid   = $rental->id;
    my $rname = $rental->name_trimmed;
    my $packet = $rental->alt_packet || $default_packet;
    my $gate_code = $rental->summary->gate_code;
    for my $g (model($c, 'Grid')->search({
                   rental_id => $rid,
               })
    ) {
        my $name = $g->name;
        my $email = join ', ', $g->notes =~ m{(\S+[@][a-zA-Z0-9.\-]+)}xmsg;
        if (empty($email)) {
            next LINE;
        }
        my $first;
        # either 'Smith, John' or 'John Smith'
        if ($name =~ m{,\s*(\S+)}xms) {
            $first = $1;
        }
        else {
            ($first) = $name =~ m{\A \s* (\S+)}xms;
        }
        my $stash_href = {
            today     => $today,
            first     => $first,
            gate_code => $gate_code,
            balance   => 0,
            program   => 0,
        };
        my $html;
        $tt->process(
            'guest_packet.tt2',
            $stash_href,
            \$html,
        ) or die "error in processing template: " . $tt->error;
        sleep 1;        # so we aren't blacklisted as spammers?
        my $alt = $packet ne $default_packet? 'Alternate ': '';
        email_letter($c,
            from    => "$string{from_title} <$string{from}>",
            to      => $email,
            subject => 'Guest Packet for your visit to MMC',
            html    => $html,
            files_to_attach => [
                "/var/Reg/documents/$packet",
            ],
            activity_msg => "${alt}Packet sent to" 
                          . " $name"
                          . ' in '
                          . "<a href='/rental/view/$rid'>$rname</a>",
        );
    }
}
