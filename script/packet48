#!/usr/bin/env perl
use strict;
use warnings;

use lib "lib";
use RetreatCenterDB;
use Util qw/
    model
    db_init
    email_letter
/;
my $c = db_init();
use Global qw/
    %string
/;
Global->init($c, 1, 1);
use Template;
use Date::Simple qw/
    today
/;

my $in_two_days_d8 = (today() + 2)->as_d8();
my @regs = model($c, 'Registration')->search({
               date_start => $in_two_days_d8,
               cancelled  => { '!=' => 'yes' },
           });
if (! @regs) {
    exit;
}
REG:
for my $reg (@regs) {
    my $person = $reg->person();
    my $email = $person->email();
    if (! $email) {
        next REG;
    }
    my $program = $reg->program();
    my $gate_code = $program->summary->gate_code();
    my $balance = $reg->balance();
    my $prepay_link = '';
    if ($balance > 0) {
        my @req_payments = $reg->req_payments();
        if (@req_payments) {
            my $code = $req_payments[0]->code();
            $prepay_link = "https://www.mountmadonna.org/cgi-bin/"
                         . "req_pay?code=$code"
                         ;
        }
    }
    my $stash_href = {
        today       => today()->format("%B %e, %Y"),
        balance     => $balance,
        first       => $person->first(),
        gate_code   => $gate_code,
        prepay_link => $prepay_link,
    };
    my $tt = Template->new({
        INTERPOLATE  => 1,
        INCLUDE_PATH => "root/static/templates/letter",
        EVAL_PERL    => 0,
    });
    my $html;
    $tt->process(
        'guest_packet.tt2',
        $stash_href,
        \$html,
    ) or die "error in processing template: " . $tt->error;
    sleep 1;        # so we aren't blacklisted as spammers?
    email_letter($c,
        from    => "$string{from_title} <$string{from}>",
        to      => $email,
        subject => 'Guest Packet for your visit to MMC',
        html    => $html,
        files_to_attach => [
            '/var/www/src/root/static/templates/letter/MMC_Guest_Packet.pdf',
        ],
        activity_msg => sprintf('Packet sent for'
                              . " <a href='/registration/view/%s'>%s</a>",
                                $reg->id, $person->name),
    );
}
