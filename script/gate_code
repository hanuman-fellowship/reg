#!/usr/bin/env perl
use strict;
use warnings;

use lib "lib";
use RetreatCenterDB;
use Util qw/
    model
    db_init
    email_letter
/;
my $c = db_init();
use Global qw/
    %string
/;
Global->init($c, 1, 1);

use Date::Simple qw/
    today
/;

my $today = today()->format("%D");
my $first_week = today()->day <= 7;
my $today8 = today()->as_d8();
my $six = (today()+6*30)->format("%D");
my $six8 = (today()+6*30)->as_d8();

#
# how many programs and rentals in the next six months need a gate code?
#
my $nprograms = model($c, 'Program')->search(
    {
        sdate => { between => [ $today8, $six8 ] },
        'summary.gate_code' => '',
    },
    {
        join => qw/ summary /,
    }
);
my $nrentals = model($c, 'Rental')->search(
    {
        sdate => { between => [ $today8, $six8 ] },
        'summary.gate_code' => '',
    },
    {
        join => qw/ summary /,
    }
);
if ($nprograms + $nrentals == 0) {
    exit;   # don't bother anyone
}
my $prog_pl = ($nprograms > 1)? "s": "";
my $verb =    ($nprograms > 1)? 'are': 'is';
my $rent_pl = ($nrentals > 1)? "s": "";
my @opt = ();
if ($nprograms + $nrentals != 0 && $first_week) {
    @opt = (cc => $string{gate_code_cc_email});
}
my $html = <<"EOH";
This is a reminder to set the gate codes!<br>
In the next 6 months there $verb $nprograms program${prog_pl} and $nrentals rental${rent_pl} without a gate code.
<p>
Click <a href="http://kali:3000/listing/gate_codes?gc_from=$today&gc_to=$six&missing_only=on">here</a>.
EOH
email_letter($c,
    from => 'Reminder <programs@mountmadonna.org>',
    to => $string{gate_code_email},
    @opt,
    bcc     => 'jon.bjornstad@gmail.com',
    subject => 'Gate Codes',
    html => $html,
);
