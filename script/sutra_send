#!/usr/bin/env perl
use strict;
use warnings;

use lib '../lib';
use Slurp qw/
    slurp
/;
use Util qw/
    db_init
    email_letter
/;
my $c = db_init();

my $dir = '/var/www/src/cgi-bin/sutras';

my @remaining;
if (open my $in, "$dir/remaining") {
    my $s = <$in>;
    @remaining = $s =~ m{(\d+)}g;
    close $in;
}
if (! @remaining) {
    @remaining = map { s{.*/}{}; $_ }
                    <$dir/[0-9]*>;
}

my $the_one = splice @remaining, rand @remaining, 1;
$the_one = "$dir/$the_one";

open my $out, ">", "$dir/remaining" or die "no write remaining";
print {$out} "@remaining\n";
close $out;

my $sutra = slurp($the_one);;

#
# html version for sending
#
$sutra =~ s{\n}{<br>\n}g;
if (substr($sutra, 0, 1) eq '^') {
    $sutra =~ s{^\^}{<pre>};
    $sutra .= "</pre>";
}

my $html = <<"EOH";
<html>
<head>
<style type="text/css">
.sutra {
    margin-left: 8mm;
    margin-top: 8mm;
    font-size: 16pt;
    font-family: Times;
}
.footnote {
    margin-top: 1in;
    font-size: 13pt;
    font-family: Times;
}
</style>
</head>
<body>
<div class=sutra>
$sutra
</div>
<div class=footnote>
To unsubscribe and for other sutra related activities click here:<br>
<a href=http://akash.mountmadonna.org/sutra>akash.mountmadonna.org/sutra</a>.
</div>
</body>
</html>
EOH

open my $addrs, "<", "$dir/addresses" or die "no addresses";
my @addrs = <$addrs>;
close $addrs;
chomp @addrs;

open my $log, '>>', "$dir/sent_log";

my $n = 0;
ADDR:
for my $em (@addrs) {
    ++$n;
    if ($n % 10 == 0) {
       sleep 60;
       # otherwise we get an authentication error?
    }

    email_letter($c,
        to => $em,
        from => 'programs@mountmadonna.org',    # ???
        subject => 'Daily Sutra from Babaji',
        html => $html,
        activity_msg => 'none',
    );
    
    print {$log} "daily sutra sent to $em ", scalar(localtime), "\n";
}
close $log;
