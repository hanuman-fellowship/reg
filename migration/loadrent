#!/usr/bin/perl
use strict;
use warnings;
use DBI;
use Getopt::Easy;

get_options("s-single");

my @seasons;
my $p = shift @ARGV;
while ($p ne 's08') {
    push @seasons, $p;
    last if $O{single};
    if ($p =~ m{^s}) {
        $p =~ s{s}{f};
    }
    else {
        my ($yr) = $p =~ m{(\d+)};
        $yr = ($yr + 1) % 100;
        $p = 's' . sprintf("%02d", $yr);
    }
}
push @seasons, 'cur' unless $O{single};

my $dbh = DBI->connect(undef, "sahadev", "JonB")
    or die "oh no\n";
$dbh->do("delete from rental");

my @fields = qw/
    id
    name
    title
    subtitle
    glnum
    sdate
    edate
    url
    webdesc
    linked
    phone
    email
    comment
    housecost_id
    n_single_bath
    n_single
    n_double_bath
    n_dble
    n_triple
    n_quad
    n_dormitory
    n_economy
    n_center_tent
    n_own_tent
    n_own_van
    n_commuting
    total_charge
    contract_sent
    sent_by
    contract_received
    received_by
    max
    start_hour
    end_hour
    coordinator_id
/;
my %lookup = qw/
    rname	name
    website	url
    desc	webdesc
/;

my $r_sql = "insert into rental ("
    . (join ",", @fields)
. ") values ("
    . (join ",", ("?") x @fields)
. ")";
my $r_sth = $dbh->prepare($r_sql) or die "no prep rental insert\n";

my %hash;

for my $seas (@seasons) {
    print "rental $seas\n";
    open my $in, "<", "new/${seas}rent.txt"
        or die "no ${seas}rent: $!\n";
    %hash = ();
    while (<$in>) {
        s{\r?\n$}{};
        my ($k, $v) = split m{\t};
        $v =~ s{^\s*|\s*$}{}g;
        if ($v eq '-') {
            $v = "";
            LINE:
            while (<$in>) {
                s{\r?\n$}{};
                s{^\s*|\s*$}{}g;
                last LINE if m{^\.$};
                $v .= "$_\n";
            }
        }
        $k = $lookup{$k} || $k;
        $hash{$k} = $v;
        if ($k eq 'linked') {
            processRental($seas);
            %hash = ();
        }
    }
    close $in;
}
$r_sth = undef;
$dbh->disconnect();

sub processRental {
    my ($season) = @_;

    for my $f (qw/ sdate edate /) {
        $hash{$f} =~ s{(..)/(..)/(....)}{$3$1$2}g;
        $hash{$f} =~ s{[/\s]}{}g;
    }
    $hash{id} = undef;
    #if ($hash{name} =~ s{\s*\*\s*$}{}) {
    #    $hash{ceu} = 'yes';
    #}
    #print "rental $season - $hash{name}\n";
    $r_sth->execute(@hash{@fields});
}
