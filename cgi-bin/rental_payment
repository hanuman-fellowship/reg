#!/usr/bin/env perl
use strict;
use warnings;

use Template;
use CGI;
my $q = CGI->new();
print $q->header();
use CGI::Carp qw/fatalsToBrowser/;
use lib '.', '../lib';
use Util qw/
    db_init
    model
    commify
    penny
    JON
    styled
/;
my $c = db_init();
use Date::Simple qw/
    date
/;
Date::Simple->default_format("%B %e '%q");

use Stripe qw/
    stripe_payment
/;

my $type = $q->param('type');   # 'deposit' or 'invoice'
if ($type !~ m{\A deposit|invoice \z}xms) {
    print "Sorry, type must be deposit or invoice.";
    exit;
}
my $code = $q->param('code');
my ($rental) = model($c, 'Rental')->search({
                   grid_code => $code,
               });
if (! $rental) {
    print "Sorry, there is no rental for code $code.";    
    exit;
}

my $sd = date($rental->sdate);
my $ed = date($rental->edate);
my $dates = $sd->format("%b %e")
          . ' to '
          . $ed->format("%b %e")
          . ', '
          . $ed->format('%Y')
          ;

my $amount = $type eq 'deposit'? $rental->deposit
            :                    penny($rental->balance)
            ;

my $name = $rental->name_trimmed;
my $person = $rental->contract_signer || $rental->coordinator();
my $rental_id = $rental->id;
my $email = $person->email;
# may not really need ANY of this demographic data!
# in the stash ... nice to get it confirmed but...
my %stash = (
    first   => $person->first,
    last    => $person->last,
    email   => $email,
    addr    => $person->addr1 . ' ' . $person->addr2,
    city    => $person->city,
    state   => $person->st_prov,
    zip     => $person->zip_post,
    country => $person->country || 'USA',
    rental  => $rental,
    phone   => $person->tel_cell || $person->tel_home,
    id      => $rental_id,
    amount  => $amount,
    amount_disp => commify($amount),
    name    => "$name - $dates",
);

$stash{payment}
    = stripe_payment(
          name        => "$name - $dates",         
          description => "\u$type",
          amount      => $amount,
          metadata    => {
            rental_id => $rental_id, 
            type      => $type,
          },
          email       => $email,
      );

Template->new(
    INTERPOLATE => 1,
)->process(
    styled('rental_payment.tt2'),
    \%stash,
);
