#!/usr/bin/env perl
use strict;
use warnings;
use CGI;
my $q = CGI->new();
print $q->header();
use lib '.', '../lib';
use Stripe qw/
    metadata
/;
my %P = metadata($q);
use Util qw/
    db_init
    model
    email_letter
    get_string
    commify
    styled
/;
my $c = db_init();
#
# initialize just the strings we will need
# here - and in compute_balance()
#
my %string;
for my $f (qw/
    min_per_day
    rental_start_hour
    rental_end_hour
    extra_hours_charge
    rental_coord_email
/) {
    $string{$f} = get_string($c, $f);
}
use Date::Simple qw/
    date
    today
/;
use Time::Simple qw/
    get_time
/;
my $now = get_time();
my $now_t24 = $now->t24;
my $today = today();
my $today_d8 = $today->as_d8();
# for what?  a record of the transaction?
my $year_mon_dir = $today->format("%Y-%m");

my $rental = model($c, 'Rental')->find($P{rental_id});
my $rental_name = $rental->name_trimmed;

model($c, 'RentalPayment')->create({
    amount         => $P{amount},
    rental_id      => $P{rental_id},
    type           => 'O',
    user_id        => 0,                 # what other user id?
    the_date       => $today_d8,
    time           => $now_t24,
    transaction_id => $P{transaction_id},
});
$rental->compute_balance();

my $person = $rental->coordinator || $rental->contract_signer;

#
# notify the arrangements people in the office
#
my $rlink = "https://akash.mountmadonna.org/rental/view/$P{rental_id}/3";
my $amount = commify($P{amount});
email_letter($c,
    to      => $string{rental_coord_email},
    from    => 'Mount Madonna Center <programs@mountmadonna.org>',
                                # what else?
    subject => "\u$P{type} payment received for $rental_name",
    html    => $person->name . " paid \$$amount"
             . " for <a href='$rlink'>$rental_name</a>",
    activity_msg => "\u$P{type} payment of \$$amount"
                  . " for <a href='/rental/view/$P{rental_id}/3'>$rental_name</a>",
);

# message to the screen
use Template;
my $tt = Template->new(
    INTERPOLATE => 1,
);
$tt->process(
    styled('rental_payment_message.tt2'),
    {
        first          => $person->first,
        type           => $P{type},
        rental_name    => $rental_name,
        amount         => $amount,
        transaction_id => $P{transaction_id},
    }
);

# and send a similar email
my $html;
$tt->process(
    'rental_payment_email.tt2',
    {
        first          => $person->first,
        type           => $P{type},
        rental_name    => $rental_name,
        amount         => $amount,
        transaction_id => $P{transaction_id},
    },
    \$html,
);
email_letter($c,
    to      => $person->email,
    from    => 'Mount Madonna Center <programs@mountmadonna.org>',
                                # what else?
    subject => "\u$P{type} payment of \$$amount received for $rental_name",
    html    => $html,
    activity_msg => "Email confirmation sent about payment for <a href='/rental/view/$P{rental_id}/3'>$rental_name</a>",
);
