#!/usr/bin/perl
#
# maybe send $data as the stash hash ref?
#

use strict;
use warnings;
use lib 'mylib/lib/perl5';
use Template;
use CGI qw/:standard/;
print header;
use CGI::Carp qw/fatalsToBrowser/;

use HousingNames;
use Date::Simple qw/
    date
    today
/;
use Time::Simple qw/
    get_time 
/;

#
# command line params
#
my $test = param('test') || 0;
my $dir  = param('dir');
my $pid  = param('id');
my %stash;
$stash{test} = $test;
$stash{dir}  = $dir;
$stash{pid}  = $pid;

sub oops {
    my ($err) = @_;
    Template->new()->process(
        'err.tt2',
        { err => $err },
    );
    exit;
}

my $only_outdoors = 0;
my $no_center_tents = 1;
$stash{no_center_tents} = 1;
if ($pid == 0) {
    # for PRs we need to check the dates
    # arr_date and dep_date from the request
    # and compare them to the dates in noPR.txt
    # and maybe set a flag to exclude indoor housing below
    #
    my $arr_date = param('arr_date');       # for PRs
    my $dep_date = param('dep_date');
    my ($sdate, $edate);
    eval {
        $sdate = Date::Simple->new($arr_date);
    };
    if (!$sdate) {
        oops "Illegal date format for arrival date: $arr_date";
    }
    my $hour = (localtime())[2];
    my $today = today();
    if ($sdate
        && ($today == $sdate
            || ($today == $sdate-1 && $hour >= 16))
    ) {
        oops "We are sorry, but registrations for Personal Retreats<br>"
             . "are not accepted after 4 p.m. on the day prior to arrival.<br>"
             . "Please register for a later date. Thank you."
             ;
    }
    eval {
        $edate = Date::Simple->new($dep_date);
    };
    if (!$edate) {
        oops "Illegal date for departure date: $dep_date";
    }
    my $ndays;
    if ($sdate && $edate) {
        $ndays = $edate-$sdate;
        if ($ndays < 0) {
            oops "Departure date is not on or after the arrival date!";
        }
    }
    if ($sdate < $today) {
        oops "Arrival date is in the past!";
    }
    my $noPRfile = "../personal/noPR.txt";
    if (-r $noPRfile) {
        open my $in, "<", $noPRfile;
        DATES:
        while (my $dateline = <$in>) {
            chomp $dateline;
            my ($sd, $ed) = $dateline =~ m{\A (\d{8})-(\d{8})}xms;
            my ($indoors) = $dateline =~ m{indoors}xms;
            $sd = date($sd);
            $ed = date($ed)-1;
            # is there overlap?
            if ($edate > $sd && $sdate <= $ed) {
                if ($indoors) {
                    $only_outdoors = 1;
                }
                else {
                    my $err .= "Sorry, you cannot stay the nights from ";
                    if ($sd->month() == $ed->month()) {
                        $err .= $sd->format("%B %e")
                             .  "-"
                             .  $ed->format("%e")
                             ;
                    }
                    else {
                        $err .= $sd->format("%B %e")
                             .  "-"
                             .  $ed->format("%B %e")
                             ;
                    }
                    $err .= '.';
                    oops $err;
                }
            }
        }
        close $in;
        if (-r "../personal/CT.txt") {
            # if center tents are not allowed on their start date 
            # we have to exclude the center tent option from the
            # housing choices.
            #
            open my $ct, "<", "../personal/CT.txt";
            my $line = <$ct>;
            close $ct;
            chomp $line;
            my ($ct_smd, $ct_emd) = split m{-}, $line;
                # ct_smd = center tent start month day
            my $pr_smd = $sdate->format("%m%d");
            if ($ct_smd <= $pr_smd && $pr_smd <= $ct_emd) {
                $no_center_tents = 0;
                $stash{no_center_tents} = 0;
            }
        }
    }
    my $dates = "$ndays day" . (($ndays > 1)? "s": "");
    $dates .= " from ";
    $dates .= $sdate->format("%A %B %e, %Y");
    $dates .= " to ";
    $dates .= $edate->format("%A %B %e, %Y");
    $stash{sdate} = $sdate;
    $stash{edate} = $edate;
    $stash{pr_dates} = $dates;
    $stash{outdoors} = $only_outdoors;
}

my $progpath = "/home/mmc/www/" . ($dir ? $dir:
                                   $test? "staging":
                                          "live");

=comment

reg1 takes a program id on the command line.
it looks it up in the regtable to get the 
various attributes of a program with that id.
it then fills in reg1.tt2 with
these various attributes and presents
it to the user.

if there is cookie information, it will also be used.

=cut

my $href = do "$progpath/progtable";
if ($@) {
    print "error in progtable: $@\n";
    exit;
}
if (! exists $href->{$pid}) {
    print "Sorry, program not found.\n";
    exit;
}
my $data = $href->{$pid};
my %cookie = cookie(-name => 'user_data');

# TODO: complain if you try to register for a program that has already happened.

for my $w (qw/
    title dates leader_names name
    canpol do_not_compute_costs dncc_why
    plink deposit
/) {
    $stash{$w} = $data->{$w};
}
for my $t (qw/
    reg_start
    reg_end
    prog_start
    prog_end
/) {
    $stash{$t} = get_time($data->{$t})->ampm();
}
$stash{pr} = $data->{title} =~ m{personal\s+retreat}i;
if ($test) {
    for my $i (1, 2) {
        $data->{"image$i"} =~ s{live}{staging};
    }
}
if ($data->{image1} && $data->{image2}) {
    $stash{two_pics} = 1;
    $stash{image1} = $data->{image1};
    $stash{image2} = $data->{image2};
}
elsif ($data->{image1}) {
    $stash{one_pic} = 1;
    $stash{image1} = $data->{image1};
}
else {
    $stash{zero_pics} = 1;
}
$stash{basicfull} = ($data->{ndays} != $data->{fulldays});
$stash{bdays}     = $data->{ndays};
$stash{fdays}     = $data->{fulldays};
if ($stash{basicfull}) {
    # muck with the dates
    ($stash{bdates}, $stash{fdates}) = split m{,}, $data->{dates};
    if ($stash{fdates} =~ m{^\s*\d}) {
        my ($month) = $stash{bdates} =~ m{([a-z]+)}i;
        $stash{fdates} = "$month " . $stash{fdates};
    }
}

if ($cookie{gender} eq "male") {
    $stash{male} = "checked";
    $stash{female}   = "";
}
elsif ($cookie{gender} eq "female") {
    $stash{female} = "checked";
    $stash{male}   = "";
}
else {
    $stash{female} = "";
    $stash{male}   = "";
}
if ($cookie{cabin_room} eq 'cabin') {
    $stash{cabin} = "checked";
    $stash{room}  = "";
}
else {
    $stash{cabin} = "";
    $stash{room}  = "checked";
}
# Carpooling
$stash{carpool}        = $cookie{carpool}? 'checked': '';
$stash{hascar}         = $cookie{hascar}? 'checked': '';
my $fw = $cookie{from_where};
if ($fw eq 'SJC') {
    $stash{sjc_checked} = 'checked';
}
elsif ($fw eq 'SFO') {
    $stash{sfo_checked} = 'checked';
}
else {
    $stash{home_checked} = 'checked';
}
$stash{from_where_display} = $stash{carpool}? 'block': 'none';

$stash{e_mailings}     = $cookie{e_mailings}? 'checked': '';
$stash{snail_mailings} = $cookie{snail_mailings}? 'checked': '';
$stash{mmi_e_mailings} = $cookie{mmi_e_mailings}? 'checked': '';
$stash{mmi_snail_mailings} = $cookie{mmi_snail_mailings}? 'checked': '';
$stash{share_mailings} = $cookie{share_mailings}? 'checked': '';
$stash{withwhom_first} = $cookie{withwhom_first} || "";
$stash{withwhom_last}  = $cookie{withwhom_last}  || "";
for my $w (qw/
    fname lname street1 street2
    city state zip country
    home work cell
    email1
/) {
    $stash{$w} = $cookie{$w} || "";
}
$stash{email2} = $cookie{email1} || "";

$stash{mft} = $data->{footnotes} =~ m{[*][*]};
$stash{ceu} = $data->{footnotes} =~ m{[*]};
$stash{ceu_license} = $cookie{ceu_license};

# housing
#
my @housekeys = grep { /^basic / } keys %$data;

if (exists $data->{next_hc} && $data->{next_hc}) {
    $stash{next_hc} = $data->{next_hc};
    $stash{through_date} = date($data->{next_date})->prev->format("%b %e");
    $stash{next_date} = date($data->{next_date})->format("%b %e");
}
my $housing_fees = "";
HOUSING_TYPE:
for my $k (sort { $data->{$a} <=> $data->{$b} } @housekeys) {
    if ($only_outdoors && $k =~ m{single|dble}xms) {
        next HOUSING_TYPE;
    }
    if ($stash{pr} && $k =~ m{center}xms && $no_center_tents) {
        next HOUSING_TYPE;
    }
    my $nk = $k;
    $nk =~ s{basic }{};
    my $full = $k;
    $full =~ s{basic}{full};
    my $next = $k;
    $next =~ s{basic}{next};
    my $select1 = $cookie{house1} eq $nk? 'checked': '';
    my $select2 = $cookie{house2} eq $nk? 'checked': '';
    $housing_fees .= <<"EOH";
<tr>

<td style="text-align: center">
<input type=radio name=house1 value='$nk' $select1>
</td>

<td style="text-align: center">
<input type=radio name=house2 value='$nk' $select2>
</td>

<td>$housing_name{$nk}</td>
EOH
    if (! $stash{do_not_compute_costs}) {
        $housing_fees .= "<td align=right width=50>\$$data->{$k}</td>\n";
        if ($stash{basicfull}) {
            $housing_fees .= "<td align=right width=70>\$$data->{$full}</td>\n";
        }
        elsif ($data->{next_hc}) {
            $housing_fees .= "<td align=right width=70>\$$data->{$next}</td>\n";
        }
    }
    $housing_fees .= "</tr>\n";
}
$stash{housing_fees} = $housing_fees;

Template->new(
    INTERPOLATE => 1,
)->process(
    "reg1.tt2",
    \%stash,
);
