#!/usr/bin/perl
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin";
use Mail::Sender;

my $dir = "$FindBin::Bin/../sutra";

my @remaining;
if (open my $in, "$dir/sutras/remaining") {
    my $s = <$in>;
    @remaining = $s =~ m{(\d+)}g;
    close $in;
}
if (! @remaining) {
    @remaining = map { s{.*/}{}; $_ }
                    <$dir/sutras/[0-9]*>;
}

my $the_one = splice @remaining, rand @remaining, 1;
$the_one = "$dir/sutras/$the_one";

open my $out, ">", "$dir/sutras/remaining" or die "no write remaining";
print {$out} "@remaining\n";
close $out;

open my $in, $the_one or die "no sutra?";
my $sutra;
{
    local $/;
    $sutra = <$in>;
    close $in;
}
#
# plain text version here
#
open my $sent, ">", "$dir/sent" or die "no sent";
print {$sent} $sutra;
close $sent;

#
# html version for sending
#
$sutra =~ s{\n}{<br>\n}g;
if (substr($sutra, 0, 1) eq '^') {
    $sutra =~ s{^\^}{<pre>};
    $sutra .= "</pre>";
}

my $html = <<"EOH";
<html>
<head>
<style type="text/css">
.sutra {
    margin-left: 8mm;
    margin-top: 8mm;
}
</style>
</head>
<body>
<div class=sutra>
$sutra
</div>
</body>
</html>
EOH

open my $addrs, "<", "$dir/addresses" or die "no addresses";
my @addrs = <$addrs>;
close $addrs;
close $addrs;
chomp @addrs;

my $ms = Mail::Sender->new({
    smtp => "localhost",
    port => 25,
});
if (! $ms) {
    die "no mail sender: ";
}
for my $em (@addrs) {
    $ms->Open({
        to       => $em,
        from     => 'Daily Sutra <no_reply@mountmadonna.org>',
        subject  => "Daily Sutra from Babaji",
        ctype    => "text/html",
        encoding => "7bit",
        # didn't seem to work :(
        # headers  => 'Errors-To: jon@logicalpoetry.com',
    })
        or die "no Mail::Sender->Open $Mail::Sender::Error";
    $ms->SendLineEnc($html);
    $ms->Close() or die "error: $Mail::Sender::Error";
}
