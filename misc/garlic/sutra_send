#!/usr/bin/perl
use strict;
use warnings;

open STDERR, ">>", "/home/mmc/www/cgi-bin/sutra_send_err";

use FindBin;
use lib "$FindBin::Bin";
use Mail::Sender;

my $dir = "$FindBin::Bin/../sutra";

my @remaining;
if (open my $in, "$dir/sutras/remaining") {
    my $s = <$in>;
    @remaining = $s =~ m{(\d+)}g;
    close $in;
}
if (! @remaining) {
    @remaining = map { s{.*/}{}; $_ }
                    <$dir/sutras/[0-9]*>;
}

my $the_one = splice @remaining, rand @remaining, 1;
$the_one = "$dir/sutras/$the_one";

open my $out, ">", "$dir/sutras/remaining" or die "no write remaining";
print {$out} "@remaining\n";
close $out;

open my $in, $the_one or die "no sutra?";
my $sutra;
{
    local $/;
    $sutra = <$in>;
    close $in;
}
#
# html version for sending
#
$sutra =~ s{\n}{<br>\n}g;
if (substr($sutra, 0, 1) eq '^') {
    $sutra =~ s{^\^}{<pre>};
    $sutra .= "</pre>";
}
#
# for the hanuman fellowship site
#
open my $sent, ">", "$dir/sent" or die "no sent";
print {$sent} $sutra;
close $sent;

my $html = <<"EOH";
<html>
<head>
<style type="text/css">
.sutra {
    margin-left: 8mm;
    margin-top: 8mm;
    font-size: 16pt;
    font-family: Times;
}
.footnote {
    margin-top: 1in;
    font-size: 13pt;
    font-family: Times;
}
</style>
</head>
<body>
<div class=sutra>
$sutra
</div>
<div class=footnote>
To unsubscribe and for other sutra related activities click here:<br>
<a href=http://www.mountmadonna.org/sutra>www.mountmadonna.org/sutra</a>.
</div>
</body>
</html>
EOH

open my $addrs, "<", "$dir/addresses" or die "no addresses";
my @addrs = <$addrs>;
close $addrs;
chomp @addrs;

#@addrs = ('jonb@logicalpoetry.com');

my $ms = Mail::Sender->new({
    smtp    => 'mail.suecenter.org',
    port    => 50,
    auth    => 'LOGIN',
    authid  => 'test@suecenter.org',
    authpwd => 'ABCdef108',
});
if (! $ms) {
    die "no mail sender";
}
ADDR:
for my $em (@addrs) {
    warn "sending to $em";
    while (1) {
        $ms->Open({
            to       => $em,
            from     => 'Daily Sutra <no_reply@mountmadonna.org>',
            subject  => "Daily Sutra from Babaji",
            ctype    => "text/html",
            encoding => "7bit",
            # the following didn't seem to work :(
            # headers  => 'Errors-To: jonb@logicalpoetry.com',
        })
            or die "no Mail::Sender->Open $Mail::Sender::Error";
        $ms->SendLineEnc($html);
        my $ret = $ms->Close();
        if (ref($ret)) {
            next ADDR;
        }
        warn "error sending to $em: $Mail::Sender::Error - sleeping an hour+";
        sleep 3610;
    }
}
